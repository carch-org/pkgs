# Maintainer: Hari Chalise <harilvfs@chalisehari.com.np>
pkgname="carch"
pkgver=4.3.1
pkgrel=1
pkgdesc="A Simple Script to Make Linux System Setups Easier"
arch=('x86_64' 'aarch64')
url="https://github.com/harilvfs/$pkgname"
license=('GPL')
options=(!strip)
depends=(
    'bash'
    'man-pages'
    'man-db'
    'fzf'
    'git'
    'wget'
    'figlet'
    'pacman'
    'noto-fonts-emoji'
    'curl'
    'tree-sitter'
    'tree-sitter-bash'
    'gcc-libs'
    'glibc'
)
optdepends=(
    'bash-completion: for Bash completion support'
    'ttf-nerd-fonts-symbols: symbols and icons'
    'ttf-jetbrains-mono-nerd: fonts'
)
conflicts=('carch-git')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
md5sums=('SKIP')
sha256sums=('bb1ea4c3c61fc3a1b2685479a654b07df86cbafee954474c1c4eeecf32c7b93b')

prepare() {
  echo "Version=$pkgver" >> "$pkgname.desktop"
  cd "$pkgname-$pkgver"
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target
  export RUSTFLAGS="-C link-arg=/usr/lib/libtree-sitter.so -C link-arg=/usr/lib/libtree-sitter-bash.so"
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

package() {

    install -Dm755 "$srcdir/$pkgname-$pkgver/target/release/$pkgname" -t "$pkgdir/usr/bin/"
       
    install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    install -Dm644 "$srcdir/$pkgname-$pkgver/README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
    
    install -Dm644 "$srcdir/$pkgname-$pkgver/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

    install -Dm644 "$srcdir/$pkgname-$pkgver/man/$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"

    install -Dm644 "$srcdir/$pkgname-$pkgver/completions/bash/$pkgname" "$pkgdir/usr/share/bash-completion/completions/$pkgname"

    install -Dm644 "$srcdir/$pkgname-$pkgver/completions/zsh/_$pkgname" "$pkgdir/usr/share/zsh/site-functions/_$pkgname"

    install -Dm644 "$srcdir/$pkgname-$pkgver/completions/fish/$pkgname.fish" "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"

    for size in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
        install -Dm644 "$srcdir/$pkgname-$pkgver/assets/icons/product_logo_${size%%x*}.png" \
            "$pkgdir/usr/share/icons/hicolor/$size/apps/carch.png"
    done
}
